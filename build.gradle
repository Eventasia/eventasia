group 'com.github.eventasia'
version = "2.0.0.SNAPSHOT"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

def ossrhUsername = project.properties['ossrhUsername'];
def ossrhPassword = project.properties['ossrhPassword'];

def signingPassword = project.properties['signing.password'];
def signingKeyId = project.properties['signing.keyId'];
def signingKeyPassword = project.properties['signing.keyPassword'];

if (System.getenv("OSSRH_USERNAME") != null) {
    ossrhUsername = System.getenv("OSSRH_USERNAME");
}
if (System.getenv("OSSRH_PASSWORD") != null) {
    ossrhPassword = System.getenv("OSSRH_PASSWORD");
}
if (System.getenv("SIGNING_PASSWORD") != null) {
    signingPassword = System.getenv("SIGNING_PASSWORD");
}
if (System.getenv("SIGNING_KEY_ID") != null) {
    signingKeyId = System.getenv("SIGNING_KEY_ID");
}
if (System.getenv("SIGNING_KEY_PASSWORD") != null) {
    signingKeyPassword = System.getenv("SIGNING_KEY_PASSWORD");
}




buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        maven { url = "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'io.codearte.nexus-staging'

subprojects {
    if (project.name == 'store' || project.name == 'stream') {
        return
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    apply plugin: 'maven'
    apply plugin: 'signing'

    apply plugin: 'org.junit.platform.gradle.plugin'

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependency "io.reactivex.rxjava2:rxjava:2.0.1"

            dependency "org.junit.jupiter:junit-jupiter-api:5.0.0-M2"
            dependency "org.junit.jupiter:junit-jupiter-engine:5.0.0-M2"
            dependency "org.assertj:assertj-core:3.6.1"
            dependency "org.reactivestreams:reactive-streams:1.0.0"
        }
    }

    dependencies {
        testCompile "org.junit.jupiter:junit-jupiter-api"
        testCompile "org.junit.jupiter:junit-jupiter-engine"
        testCompile "org.assertj:assertj-core"
    }

    plugins.withType(JavaPlugin) {
        junitPlatform {
            engines {
                include 'junit-jupiter'
            }
        }

        jar{
            version = project.version
        }
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from 'build/docs/javadoc'
        }

        task sourcesJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }

        artifacts {
            archives jar
            archives javadocJar
            archives sourcesJar
        }

        signing {
            required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
            if (isReleaseVersion) {
                sign configurations.archives
            }
        }

        //https://github.com/Codearte/gradle-nexus-staging-plugin/
        nexusStaging {
            username = ossrhUsername
            password = ossrhPassword
        }

        //http:\/\/central.sonatype.org\/pages\/ossrh-guide.html#SonatypeOSSMavenRepositoryUsageGuide-8c.AutomatingReleasesinNexus2.4
        uploadArchives {
            repositories {
                mavenDeployer {
                    if (isReleaseVersion) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name "Eventasia"
                        packaging "jar"
                        description "Eventasia is a Event Sourcing Framework for use with Spring Boot"
                        url "https://github.com/Eventasia/eventasia"

                        scm {
                            url "scm:git@github.com:Eventasia/eventasia.git"
                            connection "scm:git@github.com:Eventasia/eventasia.git"
                            developerConnection "scm:git@github.com:Eventasia/eventasia.git"
                        }

                        licenses {
                            license {
                                name "The Apache Software License, Version 2.0"
                                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                                distribution "repo"
                            }
                        }

                        developers {
                            developer {
                                id "caioquirino"
                                name "Caio Quirino da Silva"
                            }
                            developer {
                                id "fesposito"
                                name "Fábio Espósito"
                            }
                        }
                    }
                }
            }
        }

    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.allTasks.any { it instanceof Sign }) {
            def currentUser = System.getenv("USER")
            //Make secretKeyRingFile relative to the rootProject
            def secretKeyRingFile = new File("${project(':').projectDir}/${ext."signing.secretKeyRingFile"}")
            if(!secretKeyRingFile.exists()) {
              secretKeyRingFile=new File("/home/"+currentUser+"/.gnupg/secring.gpg")
            }
            allprojects {
                ext."signing.secretKeyRingFile" = secretKeyRingFile
                ext."signing.keyId" = signingKeyId
                ext."signing.keyPassword" = signingKeyPassword
                ext."signing.password" = signingPassword
            }

        }
    }
    apply plugin: 'maven-publish'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    tasks["publish"].dependsOn tasks["publishToMavenLocal"]
    tasks["build"].dependsOn tasks["publish"]
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.2.1"
}
